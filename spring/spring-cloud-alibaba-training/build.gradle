/**
 * 声明 gradle脚本自身需要使用的资源。可以声明的资源包括依赖项、第三方插件、maven仓库地址等。
 * gradle是由groovy语言编写的，支持groovy语法，可以灵活的使用已有的各种ant插件、基于jvm的类库，
 * 这也是它比maven、 ant等构建脚本强大的原因。虽然gradle支持开箱即用，但是如果你想在脚本中使用一些第三方的插件、类库等，
 * 就需要自己手动添加对这些插件、类库的 引用。而这些插件、类库又不是直接服务于项目的，而是支持其它build脚本的运行。
 * 所以你应当将这部分的引用放置在buildscript代码块中。 gradle在执行脚本时，会优先执行buildscript代码块中的内容，
 * 然后才会执行剩余的build脚本。
 */
buildscript {
  /* 定义环境变量 */
  ext {
    springIOVersion = '1.0.9.RELEASE'
    springBootVersion = '2.2.1.RELEASE'
  }
  /* 脚本运行的依赖 */
  repositories {
    def REPOSITORY_URL = 'http://maven.aliyun.com/nexus/content/groups/public/'
    all { ArtifactRepository repo ->
      if (repo instanceof MavenArtifactRepository) {
        def url = repo.url.toString()
        if (url.startsWith('https://repo1.maven.org/maven2') || url.startsWith('https://jcenter.bintray.com/')) {
          project.logger.lifecycle "Repository ${repo.url} replaced by $REPOSITORY_URL."
          remove repo
        }
      }
    }
    maven {
      mavenLocal()
      url REPOSITORY_URL
      mavenCentral()
      jcenter()
      google()
    }

  }
  /**
   * gradle所有的jar包文件坐标都在dependencies属性内放置
   * 每一个jar包都具备以下特点
   * 1.scope（作用域）：gradle支持compile、runtime、testCompile、testRuntime四种scope
   *   compile：jar包在编译期与运行期依赖。
   *   runtime：jar包在运行期依赖。
   *   testCompile：jar包在测试编译期与运行期依赖。
   *   testRuntime：jar包在测试运行期依赖。
   *   补充：
   *   providedCompile：jar包/依赖代码 仅在编译的时候需要，但是在运行时不需要依赖。
   *   providedCompile与compile,runtime区别：
   *   compile: 前提：apply plugin: 'war'或者apply plugin: 'java'
   *   providedCompile:前提：apply plugin: 'war'，若前提为'java',则使用compileOnly
   *   runtime:前提：apply plugin: 'war'
   *   以上所说的前提，如果不正确配置的话，就会遇到依赖包无法导入，以及runtime以及providedCompile无法使用的情况。
   * 2. group:与maven的groupId一致。
   *    name:与maven的artifactId一致。
   *    version:与maven的version一致。
   */
  dependencies {
    classpath "io.spring.gradle:dependency-management-plugin:${springIOVersion}"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

/**
 * 引入所有模块的配置，包括父模块,子模块
 */
allprojects {
  // jar包的group ，version配置
  group 'com.xspace.${projectName}'
  version '1.0-SNAPSHOT'

  // 指定运行环境，java
  apply plugin: 'java'
  // 指定ide环境，idea
  apply plugin: 'idea'

  // 定义全局变量
  // 定义全局变量
  ext {
    projectName = rootProject.name
    lombokVersion = '1.18.12'
    set('springCloudAlibabaVersion', "2.2.1.RELEASE")
  }

  // 私服地址
  repositories {
    def REPOSITORY_URL = 'http://maven.aliyun.com/nexus/content/groups/public/'
    all { ArtifactRepository repo ->
      if (repo instanceof MavenArtifactRepository) {
        def url = repo.url.toString()
        if (url.startsWith('https://repo1.maven.org/maven2') || url.startsWith('https://jcenter.bintray.com/')) {
          project.logger.lifecycle "Repository ${repo.url} replaced by $REPOSITORY_URL."
          remove repo
        }
      }
    }
    maven {
      mavenLocal()
      url REPOSITORY_URL
      mavenCentral()
      jcenter()
      google()
    }
  }
}

// 定义只有子工程才会执行的task
subprojects {
  // 导入使用的插件
  apply plugin: 'idea'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'
  apply plugin: 'java-library'

  // jdk编译版本
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  // 指定gradle编译时，针对java文件使用 UTF-8 编码
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  /**
   * 导入spring alibaba cloud的pom文件，能够免去自己管理版本
   * PS: 在Spring官网指导上面有另外一种配置，那种配置需要配置main class，一会说明
   */
  dependencyManagement {
    imports {
      mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
    }
  }

  /** 在这里定义公用依赖 */
  dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    /** 热部署和lombok */
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.projectlombok:lombok:1.18.12'

    /** 通用工具 */
    implementation('org.apache.commons:commons-lang3:3.10')
  }

  configurations {
    all*.exclude module: 'commons-logging'
    // 移除spring boot 默认logger依赖
    // all*.exclude module: 'spring-boot-starter-logging'
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  // 打包源码
  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }
}

